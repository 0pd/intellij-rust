

///////////////////////////////////////////////////////////////////////////////////////////////////
// Buildscript
///////////////////////////////////////////////////////////////////////////////////////////////////

buildscript {
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'org.jetbrains.intellij' version "0.0.25"
}

repositories {
    mavenCentral()
}


///////////////////////////////////////////////////////////////////////////////////////////////////
// Configurations
///////////////////////////////////////////////////////////////////////////////////////////////////

configurations {
    gen
}


///////////////////////////////////////////////////////////////////////////////////////////////////
// Projects
///////////////////////////////////////////////////////////////////////////////////////////////////

allprojects {
    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

        gen 'de.jflex:jflex:1.6.0'
        gen files('lib/gk/grammar-kit-patched.jar')

        testCompile "junit:junit:4.+"
    }

    apply plugin: 'java'
    apply plugin: 'kotlin'

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    tasks.withType(JavaCompile)     { options.encoding = 'UTF-8' }
//    tasks.withType(KotlinCompile)   { options.encoding = 'UTF-8' }

    apply plugin: 'org.jetbrains.intellij'

    intellij {
        version ideaVersion

        pluginName 'Rust'

        downloadSources downloadSources

        // publish { /* ... */ }
    }

    sourceSets {
        main {
            java {
                srcDirs = [ 'src', 'gen' ]
                include '**/*.java'
            }

            kotlin {
                srcDirs = [ 'src' ]
                include '**/*.kt'
            }

            resources.srcDirs = [ 'resources' ]
        }

        test {
            java {
                srcDirs = [ 'test' ]
                include '**/*.java'
            }

            kotlin {
                srcDirs = [ 'test' ]
                include '**/*.kt'
            }

            resources.srcDirs = [ 'testData' ]
        }
    }
}

apply plugin: 'idea'

idea {
    project {
        jdkName         = javaVersion
        languageLevel   = javaVersion
    }

    module {
        generatedSourceDirs += file('gen')
    }
}

test {
    systemProperties "idea.classpath.index.enabled": false

    useJUnit { }
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = 'full'
    }
}


///////////////////////////////////////////////////////////////////////////////////////////////////
// Tasks
///////////////////////////////////////////////////////////////////////////////////////////////////

def jflexArgs(srcFlex, targetDir) {
    return [    "--skel",   "src/org/rust/lang/core/lexer/RustLexer.skeleton",
                "-d",       targetDir,
                srcFlex
    ]
}

task generateRustLexer(type: JavaExec) {
    ext.src = "src/org/rust/lang/core/lexer/RustLexer.flex"
    ext.dst = "gen/org/rust/lang/core/lexer/"

    main = "jflex.Main"

    args = jflexArgs(src, dst)

    inputs  .file   file(src)
    outputs .dir    file(dst + "_RustLexer.java")

    classpath configurations.gen
}

task generateTomlLexer(type: JavaExec) {
    ext.src = "src/org/toml/lang/core/lexer/TomlLexer.flex"
    ext.dst = "gen/org/toml/lang/core/lexer/"

    main = "jflex.Main"

    args = jflexArgs(src, dst)

    inputs  .file   file(src)
    outputs .dir    file(dst + "_TomlLexer.java")

    classpath configurations.gen
}

task generateRustPsiAndParser(type: JavaExec) {
    ext.src     = "src/org/rust/lang/core/grammar/rust.bnf"
    ext.dstRoot = "gen"

    main = "org.intellij.grammar.Main"

    args = [ dstRoot, file(src) ]

    inputs  .file   file(src)
    outputs .dir    fileTree(dir: dstRoot + '/org/rust/lang/core/', include: '**/*.java')

    classpath configurations.gen
}

task generateTomlPsiAndParser(type: JavaExec) {
    ext.src     = "src/org/toml/lang/core/grammar/toml.bnf"
    ext.dstRoot = "gen"

    main = "org.intellij.grammar.Main"

    args = [ dstRoot, file(src) ]

    inputs  .file   file(src)
    outputs .dir    fileTree(dir: dstRoot + '/org/toml/lang/core/', include: '**/*.java')

    classpath configurations.gen
}

task generateLexers {
    dependsOn generateRustLexer, generateTomlLexer
}

task generateParsers {
    dependsOn generateRustPsiAndParser, generateTomlPsiAndParser
}

task generate {
    dependsOn generateLexers, generateParsers
}

compileJava     .dependsOn generate
compileKotlin   .dependsOn generate

compileTestJava     .dependsOn generate
compileTestKotlin   .dependsOn generate


clean {
    delete "gen"
}

///////////////////////////////////////////////////////////////////////////////////////////////////
// Misc
///////////////////////////////////////////////////////////////////////////////////////////////////

version = "${version}.$buildNumber"
